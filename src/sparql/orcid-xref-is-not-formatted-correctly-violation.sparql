PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix IAO: <http://purl.obolibrary.org/obo/IAO_>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
prefix def: <http://purl.obolibrary.org/obo/IAO_0000115>
prefix owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

SELECT ?term ?property ?value ?xref ?comment

WHERE 
{
  
VALUES ?property { oboInOwl:hasExactSynonym  oboInOwl:hasRelatedSynonym oboInOwl:hasBroadSynonym oboInOwl:hasNarrowSynonym oboInOwl:hasCloseSynonym IAO:0000115 } 
?term a owl:Class ;
   rdfs:subClassOf* obo:HP_0000001 ;
  		?property ?value .
     
     ?axiom owl:annotatedSource ?term ;
           owl:annotatedProperty ?property ;
           owl:annotatedTarget ?value ;
           oboInOwl:hasDbXref ?xref .

  FILTER  ( 
            (
              !isBlank(?xref) && 
              (
                (
                  STRSTARTS(str(?xref), "ORCiD:") || 
                  STRSTARTS(str(?xref), "orcid.org") ||
                  STRSTARTS(str(?xref), "http://orcid.org") ||
                  STRSTARTS(str(?xref), "ORCID:")
                ) || (
                  !isIRI(?xref) && STRSTARTS(str(?xref), "https://orcid.org/")
                )
              )
            )
          )
  
  BIND("The ORCID is not formatted correctly. Make sure it is an IRI, not a string, and that it starts with https://orcid.org/." as ?comment)
}
