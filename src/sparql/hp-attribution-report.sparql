PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix IAO: <http://purl.obolibrary.org/obo/IAO_>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
prefix def: <http://purl.obolibrary.org/obo/IAO_0000115>
prefix owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

SELECT ?property ?contributor ?type (count(distinct ?term) as ?count)

WHERE 
{
	{ 
      ?property a owl:AnnotationProperty .
      ?term a owl:Class ;
         rdfs:subClassOf* obo:HP_0000001 ;
              ?property ?value .

           ?axiom owl:annotatedSource ?term ;
                 owl:annotatedProperty ?property ;
                 owl:annotatedTarget ?value ;
                 oboInOwl:hasDbXref ?contributor .

        FILTER  (
            (
                isIRI(?contributor) && 
                    (
                        STRSTARTS(str(?contributor), "https://orcid.org/") || 
                        STRSTARTS(str(?contributor), "http://www.wikidata.org/entity/") ||
                        STRSTARTS(str(?contributor), "https://ror.org/021sy4w91")
                    ) 
            ) || (
                !isLiteral(?contributor) &&  STRSTARTS(str(?contributor), "HPO_CONTRIBUTOR:")
            )
        )
        BIND("XREF" as ?type)
  } UNION {
            VALUES ?property { dcterms:contributor dc:contributor IAO:0000117 oboInOwl:created_by oboInOwl:editor  dcterms:creator dc:creator }
            ?term a owl:Class ;
         		rdfs:subClassOf* obo:HP_0000001 ;
                ?property ?contributor.
    		FILTER  (
                (
                    isIRI(?contributor) && 
                        (
                            STRSTARTS(str(?contributor), "https://orcid.org/") || 
                            STRSTARTS(str(?contributor), "http://www.wikidata.org/entity/") ||
                            STRSTARTS(str(?contributor), "https://ror.org/021sy4w91")
                        ) 
                ) || (
                    !isLiteral(?contributor) &&  STRSTARTS(str(?contributor), "HPO_CONTRIBUTOR:")
                )
            )
            BIND("CREATOR" as ?type)
	}
} GROUP BY ?property ?contributor ?type
