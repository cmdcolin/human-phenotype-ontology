PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX definition: <http://purl.obolibrary.org/obo/IAO_0000115>

SELECT ?term ?label ?definition (GROUP_CONCAT(DISTINCT ?xref;SEPARATOR="|") AS ?xrefs) (GROUP_CONCAT(DISTINCT ?syns;SEPARATOR="|") AS ?synonyms) WHERE {
  ?term rdfs:subClassOf+ <http://purl.obolibrary.org/obo/HP_0030057>.
  OPTIONAL {
    ?term rdfs:label ?label .
  }
  OPTIONAL {
    ?term definition: ?definition .
  }
  OPTIONAL {
    ?term oboInOwl:hasExactSynonym ?syns .
  }
  OPTIONAL {
    ?relax a owl:Axiom ;
           owl:annotatedSource ?term ;
           owl:annotatedProperty definition: ;
           owl:annotatedTarget ?definition ;
           oboInOwl:hasDbXref ?xref .
   	FILTER(strstarts(str(?xref), "PMID:"))
  }
  FILTER(strstarts(str(?term), "http://purl.obolibrary.org/obo/HP_"))

} GROUP BY ?term ?label ?definition